Ddoc

$(SPEC_S Attributes,

$(HEADERNAV_TOC)

$(GRAMMAR
$(GNAME AttributeSpecifier):
    $(GLINK Attribute) $(D :)
    $(GLINK Attribute) $(GLINK DeclarationBlock)

$(GNAME Attribute):
    $(RELATIVE_LINK2 linkage, $(I LinkageAttribute))
    $(RELATIVE_LINK2 align, $(I AlignAttribute))
    $(RELATIVE_LINK2 deprecated, $(I DeprecatedAttribute))
    $(GLINK VisibilityAttribute)
    $(GLINK2 pragma, Pragma)
    $(RELATIVE_LINK2 static, $(D static))
    $(RELATIVE_LINK2 linkage, $(D extern))
    $(RELATIVE_LINK2 abstract, $(D abstract))
    $(D final)
    $(RELATIVE_LINK2 override, $(D override))
    $(D synchronized)
    $(RELATIVE_LINK2 auto, $(D auto))
    $(RELATIVE_LINK2 scope, $(D scope))
    $(RELATIVE_LINK2 const, $(D const))
    $(RELATIVE_LINK2 immutable, $(D immutable))
    $(RELATIVE_LINK2 inout, $(D inout))
    $(RELATIVE_LINK2 shared, $(D shared))
    $(RELATIVE_LINK2 gshared, $(D __gshared))
    $(GLINK AtAttribute)
    $(RELATIVE_LINK2 nothrow, $(D nothrow))
    $(RELATIVE_LINK2 pure, $(D pure))
    $(RELATIVE_LINK2 ref, $(D ref))
    $(RELATIVE_LINK2 return, $(D return))

$(GNAME AtAttribute):
    $(D @) $(RELATIVE_LINK2 disable, $(D disable))
    $(D @) $(RELATIVE_LINK2 nogc, $(D nogc))
    $(D @) $(D live)
    $(GLINK Property)
    $(D @) $(RELATIVE_LINK2 safe, $(D safe))
    $(D @) $(RELATIVE_LINK2 safe, $(D system))
    $(D @) $(RELATIVE_LINK2 safe, $(D trusted))
    $(GLINK UserDefinedAttribute)

$(GNAME Property):
    $(D @) $(RELATIVE_LINK2 property, $(D property))

$(GNAME DeclarationBlock):
    $(GLINK2 module, DeclDef)
    $(D {) $(GLINK2 module, DeclDefs)$(OPT) $(D })
)

        $(P Attributes are a way to modify one or more declarations.
        The general forms are:
        )

---
attribute declaration; // affects the declaration

attribute:     // affects all declarations until the end of
               // the current scope
  declaration;
  declaration;
  ...

attribute {    // affects all declarations in the block
  declaration;
  declaration;
  ...
}
---

$(H2 $(LNAME2 linkage, Linkage Attribute))

$(GRAMMAR
$(GNAME LinkageAttribute):
    $(D extern) $(D $(LPAREN)) $(GLINK LinkageType) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C++), $(GLINK2 declaration, IdentifierList) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C++), $(GLINK2 attribute, NamespaceList) $(D $(RPAREN))

$(GNAME LinkageType):
    $(D C)
    $(D C++)
    $(D D)
    $(D Windows)
    $(D System)
    $(D Objective-C)

$(GNAME NamespaceList):
    $(GLINK2 expression, ConditionalExpression)
    $(GLINK2 expression, ConditionalExpression)$(COMMA)
    $(GLINK2 expression, ConditionalExpression)$(COMMA) $(I NamespaceList)
)

        $(P D provides an easy way to call C functions and operating
        system API functions, as compatibility with both is essential.
        The $(I LinkageType) is case sensitive, and is meant to be
        extensible by the implementation ($(D they are not keywords)).
        $(D C) and $(D D) must be supplied, the others are what
        makes sense for the implementation.
        $(D C++) offers limited compatibility with C++.
        $(D Objective-C) offers limited compatibility with Objective-C,
        see the $(LINK2 objc_interface.html, Interfacing to Objective-C)
        documentation for more information.
        $(D System) is the same as $(D Windows) on Windows platforms,
        and $(D C) on other platforms.
        $(D Implementation Note:)
        for Win32 platforms, $(D Windows) should exist.
        )

        $(P C function calling conventions are
        specified by:
        )

---------------
extern (C):
    int foo(); // call foo() with C conventions
---------------
        Note that `extern(C)` can be provided for all types of
        declarations, including `struct` or `class`, even though
        there is no corresponding match on the `C` side. In that case,
        the attribute is ignored. This behavior applies for nested
        functions and nested variables as well. However, for `static` member
        methods and `static` nested functions, adding `extern(C)` will
        change the calling convention, but not the mangling.

        $(P D conventions are:)

---------------
extern (D):
---------------

        $(P Windows API conventions are:)

---------------
extern (Windows):
    void *VirtualAlloc(
        void *lpAddress,
        uint dwSize,
        uint flAllocationType,
        uint flProtect
    );
---------------

        $(P The Windows convention is distinct from the C convention only on Win32 platforms,
        where it is equivalent to the
        $(LINK2 https://en.wikipedia.org/wiki/X86_calling_conventions, stdcall) convention.)

        $(P Note that a lone $(D extern) declaration is used as a
        $(DDSUBLINK spec/declaration, extern, storage class).)

$(H3 C++ $(LNAME2 namespace, Namespaces))

        $(P The linkage form $(D extern $(LPAREN)C++, )$(I IdentifierList)$(D $(RPAREN))
        creates C++ declarations that reside in C++ namespaces. The $(I IdentifierList)
        specifies the namespaces.
        )

        ---
        extern (C++, N) { void foo(); }
        ---

        $(P refers to the C++ declaration:)

        $(CPPCODE namespace N { void foo(); })

        $(P and can be referred to with or without qualification:)

        ---
        foo();
        N.foo();
        ---

        $(P Namespaces create a new named scope that is imported into its enclosing scope.)

        ---
        extern (C++, N) { void foo(); void bar(); }
        extern (C++, M) { void foo(); }

        bar();   // ok
        foo();   // error - N.foo() or M.foo() ?
        M.foo(); // ok
        ---

        $(P Multiple identifiers in the $(I IdentifierList) create nested namespaces:)

        ---
        extern (C++, N.M) { extern (C++) { extern (C++, R) { void foo(); } } }
        N.M.R.foo();
        ---

        $(P refers to the C++ declaration:)

        $(CPPCODE namespace N { namespace M { namespace R { void foo(); } } })


$(H2 $(LNAME2 align, $(D align) Attribute))

$(GRAMMAR
$(GNAME AlignAttribute):
    $(D align)
    $(D align) $(D $(LPAREN)) $(GLINK2 expression, AssignExpression) $(D $(RPAREN))
)


        $(P Specifies the alignment of:)

        $(OL
        $(LI variables)
        $(LI struct fields)
        $(LI union fields)
        $(LI class fields)
        $(LI struct, union, and class types)
        )

        $(P $(D align) by itself
        sets it to the default, which matches the default member alignment
        of the companion C compiler.)

--------
struct S
{
  align:
    byte a;   // placed at offset 0
    int b;    // placed at offset 4
    long c;   // placed at offset 8
}
static assert(S.alignof == 8);
static assert(S.sizeof == 16);
--------

        $(P $(I AssignExpression) specifies the alignment
        which matches the behavior of the companion C compiler when non-default
        alignments are used. It must be a positive power of 2.
        )

        $(P A value of 1 means that no alignment is done;
        fields are packed together.
        )

--------
struct S
{
  align (1):
    byte a;   // placed at offset 0
    int b;    // placed at offset 1
    long c;   // placed at offset 5
}
static assert(S.alignof == 1);
static assert(S.sizeof == 13);
--------

        $(P The natural alignment of an aggregate is the maximum alignment of its
        fields. It can be overridden by setting the alignment outside of the
        aggregate.)

--------
align (2) struct S
{
  align (1):
    byte a;   // placed at offset 0
    int b;    // placed at offset 1
    long c;   // placed at offset 5
}
static assert(S.alignof == 2);
static assert(S.sizeof == 14);
--------

        $(P Setting the alignment of a field aligns it to that power of 2, regardless
        of the size of the field.)

--------
struct S
{
               byte a;  // placed at offset 0
    align (4)  byte b;  // placed at offset 4
    align (16) short c; // placed at offset 16
}
static assert(S.alignof == 16);
static assert(S.sizeof == 32);
--------


        $(P Do not align references or pointers that were allocated
        using $(GLINK2 expression, NewExpression) on boundaries that are not
        a multiple of $(D size_t). The garbage collector assumes that pointers
        and references to GC allocated objects will be on $(D size_t)
        byte boundaries.
        )

        $(UNDEFINED_BEHAVIOR If any pointers and references to GC
        allocated objects are not aligned on `size_t` byte boundaries.)

        $(P The $(I AlignAttribute) is reset to the default when
        entering a function scope or a non-anonymous struct, union, class, and restored
        when exiting that scope.
        It is not inherited from a base class.
        )



$(H2 $(LNAME2 deprecated, $(D deprecated) Attribute))

$(GRAMMAR
$(GNAME DeprecatedAttribute):
    $(D deprecated)
    $(D deprecated $(LPAREN)) $(GLINK2 expression, AssignExpression) $(D $(RPAREN))
)

        $(P It is often necessary to deprecate a feature in a library,
        yet retain it for backwards compatibility. Such
        declarations can be marked as $(D deprecated), which means
        that the compiler can be instructed to produce an error
        if any code refers to deprecated declarations:
        )

        ---------------
        deprecated
        {
            void oldFoo();
        }

        oldFoo();   // Deprecated: function test.oldFoo is deprecated
        ---------------

        $(P Optionally a string literal or manifest constant can be used
        to provide additional information in the deprecation message.
        )

        ---------------
        deprecated("Don't use bar") void oldBar();
        oldBar();   // Deprecated: function test.oldBar is deprecated - Don't use bar
        ---------------

        $(P Calling CTFE-able functions or using manifest constants is also possible.
        )

        ---------------
        import std.format;
        enum Message = format("%s and all its members are obsolete", Foobar.stringof);
        deprecated(Message) class Foobar {}
        auto f = new Foobar();   // Deprecated: class test.Foobar is deprecated - Foobar
             // and all its members are obsolete
        deprecated(format("%s is also obsolete", "This class")) class BarFoo {}
        auto bf = new BarFoo();  // Deprecated: class test.BarFoo is deprecated - This
             // class is also obsolete
        ---------------

        $(P $(D Implementation Note:) The compiler should have a switch
        specifying if $(D deprecated) should be ignored, cause a warning, or cause an error during compilation.
        )


$(H2 $(LNAME2 visibility_attributes, Visibility Attribute))

$(GRAMMAR
$(GNAME VisibilityAttribute):
    $(D private)
    $(D package)
    $(D package) $(D $(LPAREN)) $(GLINK IdentifierList) $(D $(RPAREN))
    $(D protected)
    $(D public)
    $(D export)
)

$(P Visibility is an attribute that is one of $(D private), $(D package),
`protected`, $(D public), or $(D export). They may be referred to as protection
attributes in documents predating $(LINK2 http://wiki.dlang.org/DIP22, DIP22).)

        $(P Symbols with $(D private) visibility can only be accessed from
        within the same module.
        Private member functions are implicitly final and cannot be overridden.
        )

        $(P $(D package) extends private so that package members can be accessed
        from code in other modules that are in the same package.
        If no identifier is provided, this applies to the innermost package only,
        or defaults to $(D private) if a module is not nested in a package.
        )

        $(P $(D package) may have an optional parameter in the form of a dot-separated identifier
        list which is resolved as the qualified package name. The package must be either the module's
        parent package or one of its anscestors. If this optional parameter is present, the symbol
        will be visible in the specified package and all of its descendants.
        )

        $(P $(D protected) only applies inside classes (and templates as they can be mixed in)
        and means that a symbol can only be seen by members of the same module,
        or by a derived class.
        If accessing a protected instance member through a derived class member
        function, that member can only be accessed for the object instance
        which can be implicitly cast to the same type as $(SINGLEQUOTE this).
        $(D protected) module members are illegal.
        )

        $(P $(D public) means that any code within the executable can see the member.
        It is the default visibility attribute.
        )

        $(P $(D export) means that any code outside the executable can access the
        member. $(D export) is analogous to exporting definitions from a DLL.
        )

        $(P Visibility participates in $(DDSUBLINK spec/module, name_lookup, symbol name lookup).
        )


$(H2 $(LNAME2 const, $(D const) Attribute))

        $(P The $(D const) attribute changes the type of the declared symbol from $(D T) to $(D const(T)),
        where $(D T) is the type specified (or inferred) for the introduced symbol in the absence of $(D const).
        )

        ---------------
        const int foo = 7;
        static assert(is(typeof(foo) == const(int)));

        const
        {
            double bar = foo + 6;
        }
        static assert(is(typeof(bar) == const(double)));

        class C
        {
            const void foo();
            const
            {
                void bar();
            }
            void baz() const;
        }
        pragma(msg, typeof(C.foo)); // const void()
        pragma(msg, typeof(C.bar)); // const void()
        pragma(msg, typeof(C.baz)); // const void()
        static assert(is(typeof(C.foo) == typeof(C.bar)) &&
                      is(typeof(C.bar) == typeof(C.baz)));
        ---------------

$(H2 $(LNAME2 immutable, $(D immutable) Attribute))

        $(P The $(D immutable) attribute modifies the type from $(D T) to $(D immutable(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 inout, $(D inout) Attribute))

        $(P The $(D inout) attribute modifies the type from $(D T) to $(D inout(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 shared, $(D shared) Attribute))

        $(P The $(D shared) attribute modifies the type from $(D T) to $(D shared(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 gshared, $(D __gshared) Attribute))

        $(P By default, non-immutable global declarations reside in thread local
        storage. When a global variable is marked with the $(D __gshared)
        attribute, its value is shared across all threads.)

        ---
        int foo;            // Each thread has its own exclusive copy of foo.
        __gshared int bar;  // bar is shared by all threads.
        ---

        $(P $(D __gshared) may also be applied to member variables and local
        variables. In these cases, $(D __gshared) is equivalent to $(D static),
        except that the variable is shared by all threads rather than being
        thread local.)

        ---
        class Foo
        {
            __gshared int bar;
        }

        int foo()
        {
            __gshared int bar = 0;
            return bar++; // Not thread safe.
        }
        ---

        $(P Unlike the $(D shared) attribute, $(D __gshared) provides no
        safe-guards against data races or other multi-threaded synchronization
        issues. It is the responsibility of the programmer to ensure that
        access to variables marked $(D __gshared) is synchronized correctly.)

        $(P $(D __gshared) is disallowed in safe mode.)

$(H2 $(LNAME2 disable, $(D @disable) Attribute))

$(P A reference to a declaration marked with the $(CODE @disable) attribute
causes a compile time error. This can be used to explicitly disallow certain
operations or overloads at compile time rather than relying on generating a
runtime error.)

---
@disable void foo() { }
---
---
void main() { foo(); /* error, foo is disabled */ }
---

        $(P $(DDSUBLINK spec/struct, Struct-Constructor, Disabling struct no-arg constructor)
        disallows default construction of the struct.
        )

        $(P $(DDSUBLINK spec/struct, StructPostblit, Disabling struct postblit)
        makes the struct not copyable.
        )

$(H2 $(LNAME2 safe, $(D @safe), $(D @trusted), and $(D @system) Attribute))

    $(P See $(DDSUBLINK function, function-safety, Function Safety).)

$(H2 $(LNAME2 nogc, $(D @nogc) Attribute))

    $(P See $(DDSUBLINK function, nogc-functions, No-GC Functions).)

$(H2 $(LNAME2 property, $(D @property) Attribute))

    $(P See $(DDSUBLINK spec/function, property-functions, Property Functions).)

$(H2 $(LNAME2 nothrow, $(D nothrow) Attribute))

    $(P See $(DDSUBLINK spec/function, nothrow-functions, Nothrow Functions).)

$(H2 $(LNAME2 pure, $(D pure) Attribute))

    $(P See $(DDSUBLINK spec/function, pure-functions, Pure Functions).)

$(H2 $(LNAME2 ref, $(D ref) Attribute))

    $(P See $(DDSUBLINK spec/function, ref-functions, Ref Functions).)

$(H2 $(LNAME2 return, $(D return) Attribute))

    $(P See $(DDSUBLINK spec/function, return-ref-parameters, Return Ref Parameters).)

$(H2 $(LNAME2 override, $(D override) Attribute))

        $(P The $(D override) attribute applies to virtual functions.
        It means that the function must override a function with the
        same name and parameters in a base class. The override attribute
        is useful for catching errors when a base class's member function
        gets its parameters changed, and all derived classes need to have
        their overriding functions updated.
        )

---------------
class Foo
{
    int bar();
    int abc(int x);
}

class Foo2 : Foo
{
    override
    {
        int bar(char c); // error, no bar(char) in Foo
        int abc(int x);  // ok
    }
}
---------------

$(H2 $(LNAME2 static, $(D static) Attribute))

        $(P The $(D static) attribute applies to functions and data.
        It means that the declaration does not apply to a particular
        instance of an object, but to the type of the object. In
        other words, it means there is no $(D this) reference.
        $(D static) is ignored when applied to other declarations.
        )

---------------
class Foo
{
    static int bar() { return 6; }
    int foobar() { return 7; }
}

...

Foo f = new Foo;
Foo.bar();      // produces 6
Foo.foobar();   // error, no instance of Foo
f.bar();        // produces 6;
f.foobar();     // produces 7;
---------------

$(P
        Static functions are never virtual.
)
$(P
        Static data has one instance per thread, not one per object.
)
$(P
        Static does not have the additional C meaning of being local
        to a file. Use the $(D private) attribute in D to achieve that.
        For example:
)

---------------
module foo;
int x = 3;         // x is global
private int y = 4; // y is local to module foo
---------------


$(H2 $(LNAME2 auto, $(D auto) Attribute))

        $(P The $(D auto) attribute is used when there are no other attributes
        and type inference is desired.
        )

---
auto i = 6.8;   // declare i as a double
---

    $(P For functions, the `auto` attribute means return type inference.
        See $(DDSUBLINK function, auto-functions, Auto Functions).
    )

$(H2 $(LNAME2 scope, $(D scope) Attribute))

$(P
        The $(D scope) attribute is used for local variables and for class
        declarations. For class declarations, the $(D scope) attribute creates
        a $(I scope) class.
        For local declarations, $(D scope) implements the RAII (Resource
        Acquisition Is Initialization) protocol. This means that the
        destructor for an object is automatically called when the
        reference to it goes out of scope. The destructor is called even
        if the scope is exited via a thrown exception, thus $(D scope)
        is used to guarantee cleanup.
)
$(P
        If there is more than one $(D scope) variable going out of scope
        at the same point, then the destructors are called in the reverse
        order that the variables were constructed.
)
$(P
        $(D scope) cannot be applied to globals, statics, data members, ref
        or out parameters. Arrays of $(D scope)s are not allowed, and $(D scope)
        function return values are not allowed. Assignment to a $(D scope),
        other than initialization, is not allowed.
        $(D Rationale:) These restrictions may get relaxed in the future
        if a compelling reason to appears.
)

$(H2 $(LNAME2 abstract, $(D abstract) Attribute))

$(P
        An abstract member function must be overridden by a derived class.
        Only virtual member functions may be declared abstract; non-virtual
        member functions and free-standing functions cannot be declared
        abstract.
)

$(P
        Classes become abstract if any of its virtual member functions
        are declared abstract or if they are defined within an
        abstract attribute.
        Note that an abstract class may also contain non-virtual member functions.
)

$(P
        Classes defined within an abstract attribute or with abstract member
        functions cannot be instantiated directly.
        They can only be instantiated as a base class of
        another, non-abstract, class.
)

$(P
        Member functions declared as abstract can still have function
        bodies. This is so that even though they must be overridden,
        they can still provide $(SINGLEQUOTE base class functionality),
        e.g. through $(D super.foo()) in a derived class.
        Note that the class is still abstract and cannot be instantiated directly.
)

$(H2 $(LNAME2 uda, User-Defined Attributes))

$(GRAMMAR
$(GNAME UserDefinedAttribute):
    $(D @ $(LPAREN)) $(GLINK2 expression, NamedArgumentList) $(D $(RPAREN))
    $(D @) $(GLINK_LEX Identifier)
    $(D @) $(GLINK_LEX Identifier) $(D $(LPAREN)) $(GLINK2 expression, NamedArgumentList)$(OPT) $(D $(RPAREN))
    $(D @) $(GLINK2 template, TemplateInstance)
    $(D @) $(GLINK2 template, TemplateInstance) $(D $(LPAREN)) $(GLINK2 expression, NamedArgumentList)$(OPT) $(D $(RPAREN))
)

    $(P
        User-Defined Attributes (UDA) are compile-time expressions that can be attached
        to a declaration. These attributes can then be queried, extracted, and manipulated
        at compile time. There is no runtime component to them.
    )

A user-defined attribute looks like:

---
@(3) int a;
---
---
@("string", 7) int b;

enum Foo;
@Foo int c;

struct Bar
{
    int x;
}

@Bar(3) int d;
---

        $(P
            If there are multiple UDAs in scope for a declaration, they are concatenated:
        )

---
@(1)
{
    @(2) int a;         // has UDAs (1, 2)
    @("string") int b;  // has UDAs (1, "string")
}
---

        $(P
            UDAs can be extracted into an expression tuple using $(D __traits):
        )

---
@('c') string s;
pragma(msg, __traits(getAttributes, s)); // prints tuple('c')
---

        $(P
            If there are no user-defined attributes for the symbol, an empty tuple is returned.
            The expression tuple can be turned into a manipulatable tuple:
        )

---
enum EEE = 7;
@("hello") struct SSS { }
@(3) { @(4) @EEE @SSS int foo; }

alias TP = __traits(getAttributes, foo);

pragma(msg, TP); // prints tuple(3, 4, 7, (SSS))
pragma(msg, TP[2]); // prints 7
---

        $(P
            Of course the tuple types can be used to declare things:
        )

---
TP[3] a; // a is declared as an SSS
---

        $(P
            The attribute of the type name is not the same as the attribute of the variable:
        )

---
pragma(msg, __traits(getAttributes, typeof(a))); // prints tuple("hello")
---

        $(P
            Of course, the real value of UDAs is to be able to create user-defined types with
            specific values. Having attribute values of basic types does not scale.
            The attribute tuples can be manipulated like any other tuple, and can be passed as
            the argument list to a template.
        )

        $(P
            Whether the attributes are values or types is up to the user, and whether later
            attributes accumulate or override earlier ones is also up to how the user
            interprets them.
        )
        $(P
            UDAs cannot be attached to template parameters.
        )
$(SPEC_SUBNAV_PREV_NEXT property, Properties, pragma, Pragmas)
)

Macros:
        CHAPTER=8
        TITLE=Attributes
