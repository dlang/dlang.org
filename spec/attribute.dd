Ddoc

$(SPEC_S Attributes,

$(HEADERNAV_TOC)

$(GRAMMAR
$(GNAME AttributeSpecifier):
    $(GLINK Attribute) $(D :)
    $(GLINK Attribute) $(GLINK DeclarationBlock)

$(GNAME Attribute):
    $(GLINK LinkageAttribute)
    $(GLINK AlignAttribute)
    $(GLINK DeprecatedAttribute)
    $(GLINK VisibilityAttribute)
    $(GLINK2 pragma, Pragma)
    $(RELATIVE_LINK2 static, $(D static))
    $(RELATIVE_LINK2 linkage, $(D extern))
    $(RELATIVE_LINK2 abstract, $(D abstract))
    $(RELATIVE_LINK2 final, `final`)
    $(RELATIVE_LINK2 override, $(D override))
    $(D synchronized)
    $(RELATIVE_LINK2 auto, $(D auto))
    $(RELATIVE_LINK2 scope, $(D scope))
    $(RELATIVE_LINK2 const, $(D const))
    $(RELATIVE_LINK2 immutable, $(D immutable))
    $(RELATIVE_LINK2 inout, $(D inout))
    $(RELATIVE_LINK2 shared, $(D shared))
    $(RELATIVE_LINK2 gshared, $(D __gshared))
    $(GLINK AtAttribute)
    $(GLINK FunctionAttributeKwd)
    $(RELATIVE_LINK2 ref, $(D ref))
    $(RELATIVE_LINK2 return, $(D return))

$(GNAME FunctionAttributeKwd):
    $(RELATIVE_LINK2 nothrow, $(D nothrow))
    $(RELATIVE_LINK2 pure, $(D pure))

$(GNAME AtAttribute):
    $(D @) $(RELATIVE_LINK2 disable, $(D disable))
    $(D @) $(RELATIVE_LINK2 nogc, $(D nogc))
    $(D @) $(D live)
    $(GLINK Property)
    $(D @) $(RELATIVE_LINK2 safe, $(D safe))
    $(D @) $(RELATIVE_LINK2 safe, $(D system))
    $(D @) $(RELATIVE_LINK2 safe, $(D trusted))
    $(GLINK UserDefinedAttribute)

$(GNAME Property):
    $(D @) $(RELATIVE_LINK2 property, $(D property))

$(GNAME DeclarationBlock):
    $(GLINK2 module, DeclDef)
    $(D {) $(GLINK2 module, DeclDefs)$(OPT) $(D })
)

        $(P Attributes are a way to modify one or more declarations.
        The general forms are:
        )

---
attribute declaration; // affects the declaration

attribute:     // affects all declarations until the end of
               // the current scope
  declaration;
  declaration;
  ...

attribute {    // affects all declarations in the block
  declaration;
  declaration;
  ...
}
---

$(H2 $(LNAME2 linkage, Linkage Attribute))

$(GRAMMAR
$(GNAME LinkageAttribute):
    $(D extern) $(D $(LPAREN)) $(GLINK LinkageType) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C) $(D ++) $(D ,) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C) $(D ++) $(D ,) $(GLINK2 type, QualifiedIdentifier) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C) $(D ++) $(D ,) $(GLINK NamespaceList) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C) $(D ++) $(D ,) $(D class) $(D $(RPAREN))
    $(D extern) $(D $(LPAREN)) $(D C) $(D ++) $(D ,) $(D struct) $(D $(RPAREN))

$(GNAME LinkageType):
    $(D C)
    $(D C) $(D ++)
    $(D D)
    $(D Windows)
    $(D System)
    $(D Objective) $(D -) $(D C)

$(GNAME NamespaceList):
    $(GLINK2 expression, ConditionalExpression)
    $(GLINK2 expression, ConditionalExpression)$(D ,)
    $(GLINK2 expression, ConditionalExpression)$(D ,) $(GSELF NamespaceList)
)

        $(P D provides an easy way to call C functions and operating
        system API functions, as compatibility with both is essential.
        The $(I LinkageType) is case sensitive, and is meant to be
        extensible by the implementation ($(D they are not keywords)).
        $(D C) and $(D D) must be supplied, the others are what
        makes sense for the implementation.
        $(D C++) offers limited compatibility with C++, see the
        $(LINK2 cpp_interface.html, Interfacing to C++)
        documentation for more information.
        $(D Objective-C) offers compatibility with Objective-C,
        see the $(LINK2 objc_interface.html, Interfacing to Objective-C)
        documentation for more information.
        $(D System) is the same as $(D Windows) on Windows platforms,
        and $(D C) on other platforms.
        $(D Implementation Note:)
        for Win32 platforms, $(D Windows) should exist.
        )

        $(P C function calling conventions are
        specified by:
        )

---------------
extern (C):
    int foo(); // call foo() with C conventions
---------------
        Note that `extern(C)` can be provided for all types of
        declarations, including `struct` or `class`, even though
        there is no corresponding match on the `C` side. In that case,
        the attribute is ignored. This behavior applies for nested
        functions and nested variables as well. However, for `static` member
        methods and `static` nested functions, adding `extern(C)` will
        change the calling convention, but not the mangling.

        $(P D conventions are:)

---------------
extern (D):
---------------

        $(P Windows API conventions are:)

---------------
extern (Windows):
    void *VirtualAlloc(
        void *lpAddress,
        uint dwSize,
        uint flAllocationType,
        uint flProtect
    );
---------------

        $(P The Windows convention is distinct from the C convention only on Win32 platforms,
        where it is equivalent to the
        $(LINK2 https://en.wikipedia.org/wiki/X86_calling_conventions, stdcall) convention.)

        $(P Note that a lone $(D extern) declaration is used as a
        $(DDSUBLINK spec/declaration, extern, storage class).)

$(H3 C++ $(LNAME2 namespace, Namespaces))

        $(P The linkage form $(D extern $(LPAREN)C++, )$(I QualifiedIdentifier)$(D $(RPAREN))
        creates C++ declarations that reside in C++ namespaces. The $(I QualifiedIdentifier)
        specifies the namespaces.
        )

        ---
        extern (C++, N) { void foo(); }
        ---

        $(P refers to the C++ declaration:)

        $(CPPCODE namespace N { void foo(); })

        $(P and can be referred to with or without qualification:)

        ---
        foo();
        N.foo();
        ---

        $(P Namespaces create a new named scope that is imported into its enclosing scope.)

        ---
        extern (C++, N) { void foo(); void bar(); }
        extern (C++, M) { void foo(); }

        bar();   // ok
        foo();   // error - N.foo() or M.foo() ?
        M.foo(); // ok
        ---

        $(P Multiple identifiers in the $(I QualifiedIdentifier) create nested namespaces:)

        ---
        extern (C++, N.M) { extern (C++) { extern (C++, R) { void foo(); } } }
        N.M.R.foo();
        ---

        $(P refers to the C++ declaration:)

        $(CPPCODE namespace N { namespace M { namespace R { void foo(); } } })


$(H2 $(LNAME2 align, $(D align) Attribute))

$(GRAMMAR
$(GNAME AlignAttribute):
    $(D align)
    $(D align) $(D $(LPAREN)) $(GLINK2 expression, AssignExpression) $(D $(RPAREN))
)


        $(P Specifies the alignment of:)

        $(OL
        $(LI variables)
        $(LI struct fields)
        $(LI union fields)
        $(LI class fields)
        $(LI struct, union, and class types)
        )

        $(P $(D align) by itself
        sets it to the default, which matches the default member alignment
        of the companion C compiler.)

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
--------
struct S
{
  align:
    byte a;   // placed at offset 0
    int b;    // placed at offset 4
    long c;   // placed at offset 8
}
static assert(S.alignof == 8);
static assert(S.c.offsetof == 8);
static assert(S.sizeof == 16);
--------
)
        $(P The $(I AssignExpression) specifies the alignment
        which matches the behavior of the companion C compiler when non-default
        alignments are used. It must be a positive power of 2.
        )

        $(P A value of 1 means that no alignment is done;
        fields are packed together.
        )

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
--------
struct S
{
  align (1):
    byte a;   // placed at offset 0
    int b;    // placed at offset 1
    long c;   // placed at offset 5
}
static assert(S.alignof == 1);
static assert(S.c.offsetof == 5);
static assert(S.sizeof == 13);
--------
)
        $(P The natural alignment of an aggregate is the maximum alignment of its
        fields. It can be overridden by setting the alignment outside of the
        aggregate.)

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
--------
align (2) struct S
{
  align (1):
    byte a;   // placed at offset 0
    int b;    // placed at offset 1
    long c;   // placed at offset 5
}
static assert(S.alignof == 2);
static assert(S.c.offsetof == 5);
static assert(S.sizeof == 14);
--------
)
        $(P Setting the alignment of a field aligns it to that power of 2, regardless
        of the size of the field.)

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
--------
struct S
{
               byte a;  // placed at offset 0
    align (4)  byte b;  // placed at offset 4
    align (16) short c; // placed at offset 16
}
static assert(S.alignof == 16);
static assert(S.c.offsetof == 16);
static assert(S.sizeof == 32);
--------
)
        $(P The $(I AlignAttribute) is reset to the default when
        entering a function scope or a non-anonymous struct, union, class, and restored
        when exiting that scope.
        It is not inherited from a base class.
        )

        $(P See also: $(DDSUBLINK spec/struct, struct_layout, Struct Layout).)

$(H3 $(LNAME2 align_gc, GC Compatibility))

        $(P Do not align references or pointers that were allocated
        using $(GLINK2 expression, NewExpression) on boundaries that are not
        a multiple of $(D size_t). The garbage collector assumes that pointers
        and references to GC allocated objects will be on $(D size_t)
        byte boundaries.
        )

$(SPEC_RUNNABLE_EXAMPLE_FAIL
---
struct S
{
  align(1):
    byte b;
    int* p;
}

static assert(S.p.offsetof == 1);

@safe void main()
{
    S s;
    s.p = new int; // error: can't modify misaligned pointer in @safe code
}
---
)
        $(UNDEFINED_BEHAVIOR If any pointers and references to GC
        allocated objects are not aligned on `size_t` byte boundaries.)


$(H2 $(LNAME2 deprecated, $(D deprecated) Attribute))

$(GRAMMAR
$(GNAME DeprecatedAttribute):
    $(D deprecated)
    $(D deprecated $(LPAREN)) $(GLINK2 expression, AssignExpression) $(D $(RPAREN))
)

        $(P It is often necessary to deprecate a feature in a library,
        yet retain it for backwards compatibility. Such
        declarations can be marked as $(D deprecated), which means
        that the compiler can be instructed to produce an error
        if any code refers to deprecated declarations:
        )

        ---------------
        deprecated
        {
            void oldFoo();
        }

        oldFoo();   // Deprecated: function test.oldFoo is deprecated
        ---------------

        $(P Optionally a string literal or manifest constant can be used
        to provide additional information in the deprecation message.
        )

        ---------------
        deprecated("Don't use bar") void oldBar();
        oldBar();   // Deprecated: function test.oldBar is deprecated - Don't use bar
        ---------------

        $(P Calling CTFE-able functions or using manifest constants is also possible.
        )

        ---------------
        import std.format;
        enum Message = format("%s and all its members are obsolete", Foobar.stringof);
        deprecated(Message) class Foobar {}
        auto f = new Foobar();   // Deprecated: class test.Foobar is deprecated - Foobar
             // and all its members are obsolete
        deprecated(format("%s is also obsolete", "This class")) class BarFoo {}
        auto bf = new BarFoo();  // Deprecated: class test.BarFoo is deprecated - This
             // class is also obsolete
        ---------------

        $(P $(D Implementation Note:) The compiler should have a switch
        specifying if $(D deprecated) should be ignored, cause a warning, or cause an error during compilation.
        )


$(H2 $(LNAME2 visibility_attributes, Visibility Attribute))

$(GRAMMAR
$(GNAME VisibilityAttribute):
    $(D private)
    $(D package)
    $(D package) $(D $(LPAREN)) $(GLINK2 type, QualifiedIdentifier) $(D $(RPAREN))
    $(D protected)
    $(D public)
    $(D export)
)

$(P Visibility is an attribute that is one of $(D private), $(D package),
`protected`, $(D public), or $(D export). They may be referred to as protection
attributes in documents predating $(LINK2 http://wiki.dlang.org/DIP22, DIP22).)

        $(P Symbols with $(D private) visibility can only be accessed from
        within the same module.
        Private member functions are implicitly $(DDSUBLINK spec/function, final, `final`)
        and cannot be overridden.
        )

        $(P $(D package) extends private so that package members can be accessed
        from code in other modules that are in the same package.
        If no identifier is provided, this applies to the innermost package only,
        or defaults to $(D private) if a module is not nested in a package.
        )

        $(P $(D package) may have an optional parameter in the form of a dot-separated identifier
        list which is resolved as the qualified package name. The package must be either the module's
        parent package or one of its anscestors. If this optional parameter is present, the symbol
        will be visible in the specified package and all of its descendants.
        )

        $(P $(D protected) only applies inside classes (and templates as they can be mixed in)
        and means that a symbol can only be seen by members of the same module,
        or by a derived class.
        If accessing a protected instance member through a derived class member
        function, that member can only be accessed for the object instance
        which can be implicitly cast to the same type as $(SINGLEQUOTE this).
        $(D protected) module members are illegal.
        )

        $(P $(D public) means that any code within the executable can see the member.
        It is the default visibility attribute.
        )

        $(P $(D export) means that any code outside the executable can access the
        member. $(D export) is analogous to exporting definitions from a DLL.
        )

        $(P Visibility participates in $(DDSUBLINK spec/module, name_lookup, symbol name lookup).
        )


$(H2 $(LNAME2 const, $(D const) Attribute))

        $(P The $(D const) attribute changes the type of the declared symbol from $(D T) to $(D const(T)),
        where $(D T) is the type specified (or inferred) for the introduced symbol in the absence of $(D const).
        )

        ---------------
        const int foo = 7;
        static assert(is(typeof(foo) == const(int)));

        const
        {
            double bar = foo + 6;
        }
        static assert(is(typeof(bar) == const(double)));

        class C
        {
            const void foo();
            const
            {
                void bar();
            }
            void baz() const;
        }
        pragma(msg, typeof(C.foo)); // const void()
        pragma(msg, typeof(C.bar)); // const void()
        pragma(msg, typeof(C.baz)); // const void()
        static assert(is(typeof(C.foo) == typeof(C.bar)) &&
                      is(typeof(C.bar) == typeof(C.baz)));
        ---------------

$(H2 $(LNAME2 immutable, $(D immutable) Attribute))

        $(P The $(D immutable) attribute modifies the type from $(D T) to $(D immutable(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 inout, $(D inout) Attribute))

        $(P The $(D inout) attribute modifies the type from $(D T) to $(D inout(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 shared, $(D shared) Attribute))

        $(P The $(D shared) attribute modifies the type from $(D T) to $(D shared(T)),
        the same way as $(D const) does.
        )

$(H2 $(LNAME2 gshared, $(D __gshared) Attribute))

        $(P By default, non-immutable global declarations reside in thread local
        storage. When a global variable is marked with the $(D __gshared)
        attribute, its value is shared across all threads.)

        ---
        int foo;            // Each thread has its own exclusive copy of foo.
        __gshared int bar;  // bar is shared by all threads.
        ---

        $(P $(D __gshared) may also be applied to member variables and local
        variables. In these cases, $(D __gshared) is equivalent to $(D static),
        except that the variable is shared by all threads rather than being
        thread local.)

        ---
        class Foo
        {
            __gshared int bar;
        }

        int foo()
        {
            __gshared int bar = 0;
            return bar++; // Not thread safe.
        }
        ---

        $(P Unlike the $(D shared) attribute, $(D __gshared) provides no
        safe-guards against data races or other multi-threaded synchronization
        issues. It is the responsibility of the programmer to ensure that
        access to variables marked $(D __gshared) is synchronized correctly.)

        $(P $(D __gshared) is disallowed in safe mode.)

$(H2 $(LNAME2 disable, $(D @disable) Attribute))

$(P A reference to a declaration marked with the $(CODE @disable) attribute
causes a compile time error. This can be used to explicitly disallow certain
operations or overloads at compile time rather than relying on generating a
runtime error.)

---
@disable void foo() { }
---
---
void main() { foo(); /* error, foo is disabled */ }
---

        $(P $(DDSUBLINK spec/struct, disable_default_construction, `@disable this();`)
        inside a struct disallows default construction.
        )

        $(P $(DDSUBLINK spec/struct, disable-copy, Disabling a struct copy constructor)
        makes the struct not copyable.
        )

$(H2 $(LNAME2 safe, $(D @safe), $(D @trusted), and $(D @system) Attribute))

    $(P See $(DDSUBLINK spec/function, function-safety, Function Safety).)

$(H2 $(LNAME2 nogc, $(D @nogc) Attribute))

    $(P See $(DDSUBLINK spec/function, nogc-functions, No-GC Functions).)

$(H2 $(LNAME2 property, $(D @property) Attribute))

    $(P See $(DDSUBLINK spec/function, property-functions, Property Functions).)

$(H2 $(LNAME2 nothrow, $(D nothrow) Attribute))

    $(P See $(DDSUBLINK spec/function, nothrow-functions, Nothrow Functions).)

$(H2 $(LNAME2 pure, $(D pure) Attribute))

    $(P See $(DDSUBLINK spec/function, pure-functions, Pure Functions).)

$(H2 $(LNAME2 ref, $(D ref) Attribute))

    $(P See $(DDSUBLINK spec/function, ref-functions, Ref Functions).)

$(H2 $(LNAME2 return, $(D return) Attribute))

    $(P See $(DDSUBLINK spec/function, return-ref-parameters, Return Ref Parameters).)

$(H2 $(LNAME2 override, $(D override) Attribute))

    $(P See $(DDSUBLINK spec/function, virtual-functions, Virtual Functions).)

$(H2 $(LNAME2 static, $(D static) Attribute))

        $(P The $(D static) attribute applies to functions and data.)

        $(P Inside an aggregate type, a `static`
        declaration does not apply to a particular
        instance of an object, but to the type of the object. In
        other words, it means there is no $(D this) reference.
        $(D static) is ignored when applied to other declarations.
        )

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
---------------
class Foo
{
    static int x;
    static int bar() { return x; }
    int foobar() { return 7; }
}

Foo.x = 6; // no instance needed
assert(Foo.bar() == 6);
//Foo.foobar(); // error, no instance of Foo

Foo f = new Foo;
assert(f.bar() == 6);
assert(f.foobar() == 7);
---------------
)

$(P
        Static functions are never $(DDSUBLINK spec/function, virtual-functions, virtual).
)
$(P
        Static data has one instance per thread, not one per object.
)
$(P A static $(DDSUBLINK spec/function, nested, nested function) cannot
access variables in the parent scope.)

$(P Inside a function, a $(DDSUBLINK spec/function, local-static-variables,
static local variable) persists after the function returns.)

$(P
        Static does not have the additional C meaning of being local
        to a file. Use the $(RELATIVE_LINK2 VisibilityAttribute, `private`)
        attribute in D to achieve that.
        For example:
)

---------------
module foo;
int x = 3;         // x is global
private int y = 4; // y is local to module foo
---------------


$(H2 $(LNAME2 auto, $(D auto) Attribute))

        $(P The $(D auto) attribute is used when there are no other attributes
        and type inference is desired.
        )

---
auto i = 6.8;   // declare i as a double
---

    $(P For functions, the `auto` attribute means return type inference.
        See $(DDSUBLINK spec/function, auto-functions, Auto Functions).
    )

$(H2 $(LNAME2 scope, $(D scope) Attribute))

$(P
        The `scope` attribute signifies a variable's pointer values will not escape the scope that the variable is declared in.
)

$(P
        If the variable has a type that does not contain any indirections, the `scope` attribute is ignored.
)

$(P
        When applied to a global variable, `scope` is also ignored, since there is no scope larger than global scope that pointers could escape to.
)

$(SPEC_RUNNABLE_EXAMPLE_COMPILE
---
scope int* x; // scope ignored, global variable

void main()
{
    static int* x;  // scope ignored, global variable
    scope float y;  // scope ignored, no indirections
    scope int[2] z; // scope ignored, static array is value type
    scope int[] w;  // scope dynamic array
}
---
)

$(P
        When applied to a local variable with a type that has indirections,
        its value may not be assigned to a variable with longer lifetime:
)
        $(UL
            $(LI Variables outside the $(DDSUBLINK spec/statement, scope-statement, Scope Statement) that the variable is declared in)
            $(LI Variables declared before the `scope` variable$(COMMA) since local variables are destructed in the reverse order that they are declared in)
            $(LI `__gshared` or `static` variables)
        )

$(P
        Other operations implicitly assigning them to variables with longer lifetime are also disallowed:
)
        $(UL
            $(LI Returning a `scope` variable from a function)
            $(LI Assigning a `scope` variable to a non-scope parameter by calling a function)
            $(LI Putting a `scope` variable in an array literal)
        )
$(P
        The `scope` attribute is part of the variable declaration, not of the type, and it only applies to the first level of indirection.
        For example, it is impossible to declare a variable as a dynamic array of scope pointers, because `scope` only applies to the `.ptr`
        of the array itself, not its elements. `scope` affects various types as follows:
)

        $(TABLE_2COLS ,
        $(THEAD Type of local variable, What `scope` applies to)

        $(TROW Any $(DDSUBLINK spec/type, basic-data-types, Basic Data Type), nothing)
        $(TROW $(DDSUBLINK spec/arrays, pointers, Pointer) `T*`, the pointer value)
        $(TROW $(DDSUBLINK spec/arrays, dynamic-arrays, Dynamic Array) `T[]`, the `.ptr` to the elements)
        $(TROW $(DDSUBLINK spec/arrays, static-arrays, Static Array) `T[n]`, each element `T`)
        $(TROW $(GLINK2 hash-map, Associative Array) `K[V]`, the pointer to the implementation defined structure)
        $(TROW `struct` or `union`, each of its member variables)
        $(TROW `function` pointer, the pointer value)
        $(TROW $(DDSUBLINK spec/function, closures, delegate), both the `.funcptr` and `.ptr` (closure context) pointer values)
        $(TROW `class` or `interface`, the class reference)
        $(TROW $(GLINK2 enum, enum), the base type)
        )

---
struct S
{
    string str; // note: string = immutable(char)[]
    string* strPtr;
}

string escape(scope S s, scope S* sPtr, scope string[2] sarray, scope string[] darray)
{
    return s.str;        // invalid, scope applies to struct members
    return *s.strPtr;    // valid, scope struct member is dereferenced
    return sPtr.str;     // valid, struct pointer is dereferenced
    return *sPtr.strPtr; // valid, two pointers are dereferenced
    return sarray[0];    // invalid, scope applies to static array elements
    return sarray[1];    // invalid, ditto
    return darray[0];    // valid, scope applies to array pointer, not elements
}
---

$(P
    A "`scope` value" is the value of a `scope` variable, or a generated value pointing to stack allocated memory.
    Such values are generated by $(DDSUBLINK spec/arrays, slicing, Slicing) or creating $(DDSUBLINK spec/arrays, pointers, Pointers)
    to a variable that is (or may be) allocated on the stack:
)
    $(UL
        $(LI A function parameter)
        $(LI A local variable)
        $(LI A struct member accessed through the implicit `this` parameter)
        $(LI The return value of a $(DDSUBLINK spec/function, ref-functions, Ref Function))
    )

$(P
    The variadic parameter from $(DDSUBLINK spec/function, typesafe_variadic_functions, Typesafe Variadic Functions) is also a scope value,
    since the arguments are passed on the stack.
)

$(P
    When a local variable is assigned a `scope` value, it is inferred `scope`, even when the variable has an explicit type and does not use the `auto` keyword.
)

---
@safe:
ref int[2] identity(return ref int[2] x) {return x;}

int* escape(int[2] y, scope int* z)
{
    int x;

    auto  xPtr  = &x;            // inferred `scope int*`
    int[] yArr  = identity(y)[]; // inferred `scope int[]`
    int*  zCopy = z;             // inferred `scope int*`

    return zCopy; // error
}

void variadic(int[] a...)
{
    int[] x = a; // inferred `scope int[]`
}

struct S
{
    int x;

    // this method may be called on a stack-allocated instance of S
    void f()
    {
        int* p = &x; // inferred `scope int* p`
        int* q = &this.x; // equivalent
    }
}
---

$(P
    $(DDSUBLINK spec/function, scope-parameters, Scope Parameters) are treated the same as scope local variables,
    except that returning them is allowed when the function has $(DDSUBLINK spec/function, function-attribute-inference, Function Attribute Inference).
    In that case, they are inferred as $(DDSUBLINK spec/function, return-scope-parameters, Return Scope Parameters).
)

$(H3 $(LNAME2 scope-class-var, $(D scope) variables with `class` type))
$(P
        When used on variables with a `class` type, `scope` signifies the RAII
        (Resource Acquisition Is Initialization) protocol.
        This means that the destructor for an object is automatically called when the
        reference to it goes out of scope. The destructor is called even
        if the scope is exited via a thrown exception, thus $(D scope)
        is used to guarantee cleanup.
)
$(P
        When a class is constructed with `new` and assigned to a local `scope` variable,
        it may be allocated on the stack and permitted in a `@nogc` context.
)
$(P
        If there is more than one `scope` class variable going out of scope
        at the same point, then the destructors are called in the reverse
        order that the variables were constructed.
)
$(P
        Assignment to a $(D scope) variable with class type,
        other than initialization, is not allowed, because that would complicate
        proper destruction of the variable.
)

---
class C {}

void main() @nogc
{
    {
        scope c0 = new C(); // allocated on the stack
        scope c1 = new C();

        c1 = c0; // not allowed

        // destructor of `c1` and `c0` are called here in that order
    }
}
---

$(H2 $(LNAME2 abstract, $(D abstract) Attribute))

$(P
        An abstract member function must be overridden by a derived class.
        Only virtual member functions may be declared abstract; non-virtual
        member functions and free-standing functions cannot be declared
        abstract.
)

$(P
        Classes become abstract if any of its virtual member functions
        are declared abstract or if they are defined within an
        abstract attribute.
        Note that an abstract class may also contain non-virtual member functions.
)

$(P
        Classes defined within an abstract attribute or with abstract member
        functions cannot be instantiated directly.
        They can only be instantiated as a base class of
        another, non-abstract, class.
)

$(P
        Member functions declared as abstract can still have function
        bodies. This is so that even though they must be overridden,
        they can still provide $(SINGLEQUOTE base class functionality),
        e.g. through $(D super.foo()) in a derived class.
        Note that the class is still abstract and cannot be instantiated directly.
)

$(H2 $(LNAME2 final, `final` Attribute))

$(UL
$(LI A class can be declared $(DDSUBLINK spec/class, final, `final`) to prevent
    subclassing.)
$(LI A class method can be declared $(DDSUBLINK spec/function, final, `final`)
    to prevent a derived class overriding it.)
$(LI Interfaces can define $(DDSUBLINK spec/interface, method-bodies, `final` methods).)
)

$(H2 $(LNAME2 mustuse-attribute, `@mustuse` Attribute))

    $(P
        The `@mustuse` attribute is a compiler-recognized $(RELATIVE_LINK2 uda,
        UDA) defined in the D runtime module $(DPLLINK phobos/core_attribute.html,
        `core.attribute`).
    )


    $(P
        An expression is considered to be discarded if and only if either of the
        following is true:
    )
    $(UL
        $(LI
            it is the top-level $(GLINK2 expression, Expression) in an $(GLINK2
            statement, ExpressionStatement), or
        )
        $(LI
            it is the $(GLINK2 expression, AssignExpression) on the left-hand
            side of the comma in a $(GLINK2 expression, CommaExpression).
        )
    )

    $(P
        It is a compile-time error to discard an expression if all of the
        following are true:
    )

    $(UL
        $(LI
            it is not an assignment expression, an increment expression, or a
            decrement expression; and
        )

        $(LI
            its type is a `struct` or `union` type whose declaration is
            annotated with `@mustuse`.
        )
    )

    $(P
        "Assignment expression" means either a $(DDSUBLINK spec/expression,
        simple_assignment_expressions, simple assignment expression) or an
        $(DDSUBLINK spec/expression, assignment_operator_expression, assignment
        operator expression).
    )

    $(P
        "Increment expression" means a $(GLINK2 expression, UnaryExpression) or
        $(GLINK2 expression, PostfixExpression) whose operator is `++`.
    )

    $(P
        "Decrement expression" means a $(GLINK2 expression, UnaryExpression) or
        $(GLINK2 expression, PostfixExpression) whose operator is `--`.
    )

    $(P
        It is a compile-time error to attach `@mustuse` to a function
        declaration or to any aggregate declaration other than a `struct` or
        `union` declaration. The purpose of this rule is to reserve such usage
        for possible future expansion.
    )

$(H2 $(LNAME2 uda, User-Defined Attributes))

$(GRAMMAR
$(GNAME UserDefinedAttribute):
    $(D @ $(LPAREN)) $(GLINK2 expression, ArgumentList) $(D $(RPAREN))
    $(D @) $(GLINK_LEX Identifier)
    $(D @) $(GLINK_LEX Identifier) $(D $(LPAREN)) $(GLINK2 expression, ArgumentList)$(OPT) $(D $(RPAREN))
    $(D @) $(GLINK2 template, TemplateInstance)
    $(D @) $(GLINK2 template, TemplateInstance) $(D $(LPAREN)) $(GLINK2 expression, ArgumentList)$(OPT) $(D $(RPAREN))
)

    $(P
        User-Defined Attributes (UDA) are compile-time expressions that can be attached
        to a declaration. These attributes can then be queried, extracted, and manipulated
        at compile time. There is no runtime component to them.
    )

A user-defined attribute looks like:

---
@(3) int a;
---
---
@("string", 7) int b;

enum Foo;
@Foo int c;

struct Bar
{
    int x;
}

@Bar(3) int d;
---

        $(P
            If there are multiple UDAs in scope for a declaration, they are concatenated:
        )

---
@(1)
{
    @(2) int a;         // has UDAs (1, 2)
    @("string") int b;  // has UDAs (1, "string")
}
---

        $(P
            UDAs can be extracted into an expression tuple using $(D __traits):
        )

---
@('c') string s;
pragma(msg, __traits(getAttributes, s)); // prints tuple('c')
---

        $(P
            If there are no user-defined attributes for the symbol, an empty tuple is returned.
            The expression tuple can be turned into a manipulatable tuple:
        )

---
enum EEE = 7;
@("hello") struct SSS { }
@(3) { @(4) @EEE @SSS int foo; }

alias TP = __traits(getAttributes, foo);

pragma(msg, TP); // prints tuple(3, 4, 7, (SSS))
pragma(msg, TP[2]); // prints 7
---

        $(P
            Of course the tuple types can be used to declare things:
        )

---
TP[3] a; // a is declared as an SSS
---

        $(P
            The attribute of the type name is not the same as the attribute of the variable:
        )

---
pragma(msg, __traits(getAttributes, typeof(a))); // prints tuple("hello")
---

        $(P
            Of course, the real value of UDAs is to be able to create user-defined types with
            specific values. Having attribute values of basic types does not scale.
            The attribute tuples can be manipulated like any other tuple, and can be passed as
            the argument list to a template.
        )

        $(P
            Whether the attributes are values or types is up to the user, and whether later
            attributes accumulate or override earlier ones is also up to how the user
            interprets them.
        )

        $(P
            If a UDA is attached to a template declaration, then it will be automatically
            attached to all direct members of instances of that template. If any of those
            members are templates themselves, this rule applies recursively:
        )

---
@("foo") template Outer(T)
{
    struct S
    {
        int x;
    }
    int y;
    void fun() {}
    @("bar") template Inner(U)
    {
        int z;
    }
}

pragma(msg, __traits(getAttributes, Outer!int.S));
// prints tuple("foo")
pragma(msg, __traits(getAttributes, Outer!int.S.x));
// prints tuple()
pragma(msg, __traits(getAttributes, Outer!int.y));
// prints tuple("foo")
pragma(msg, __traits(getAttributes, Outer!int.fun));
// prints tuple("foo")
pragma(msg, __traits(getAttributes, Outer!int.Inner));
// prints tuple("foo", "bar")
pragma(msg, __traits(getAttributes, Outer!int.Inner!int.z));
// prints tuple("foo", "bar")
---

        $(P
            UDAs cannot be attached to template parameters.
        )
$(SPEC_SUBNAV_PREV_NEXT property, Properties, pragma, Pragmas)
)

Macros:
        CHAPTER=8
        TITLE=Attributes
