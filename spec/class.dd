Ddoc

$(SPEC_S Classes,

$(HEADERNAV_TOC)

        $(P The object-oriented features of D all come from classes. The class
        hierarchy
        has as its root the class Object. Object defines a minimum level of functionality
        that each derived class has, and a default implementation for that functionality.
        )

        $(P Classes are programmer defined types. Support for classes are what
        make D an object oriented language, giving it encapsulation, inheritance,
        and polymorphism. D classes support the single inheritance paradigm, extended
        by adding support for interfaces. Class objects are instantiated by reference
        only.
        )

        $(P A class can be exported, which means its name and all its
        non-private
        members are exposed externally to the DLL or EXE.
        )

        $(P A class declaration is defined:
        )

$(GRAMMAR
$(GNAME ClassDeclaration):
    $(D class) $(GLINK_LEX Identifier) $(D ;)
    $(D class) $(GLINK_LEX Identifier) $(GLINK BaseClassList)$(OPT) $(GLINK2 struct, AggregateBody)
    $(GLINK2 template, ClassTemplateDeclaration)

$(GNAME BaseClassList):
    $(D :) $(GLINK SuperClass)
    $(D :) $(GLINK SuperClass) $(D ,) $(GLINK Interfaces)
    $(D :) $(GLINK Interfaces)

$(GNAME SuperClass):
    $(GLINK2 type, BasicType)

$(GNAME Interfaces):
    $(GLINK Interface)
    $(GLINK Interface) $(D ,) $(GSELF Interfaces)

$(GNAME Interface):
    $(GLINK2 type, BasicType)
)

$(P Classes consist of:)

$(UL
        $(LI a super class)
        $(LI interfaces)
        $(LI a nested anonymous metaclass for classes declared with `extern (Objective-C)`)
        $(LI dynamic fields)
        $(LI static fields)
        $(LI types)
        $(LI $(RELATIVE_LINK2 synchronized-classes, an optional synchronized attribute))
        $(LI $(RELATIVE_LINK2 member-functions, member functions)
        $(UL
            $(LI static member functions)
            $(LI $(DDSUBLINK spec/function, virtual-functions, Virtual Functions))
            $(LI $(RELATIVE_LINK2 constructors, Constructors))
            $(LI $(RELATIVE_LINK2 destructors, Destructors))
            $(LI $(RELATIVE_LINK2 static-constructor, Static Constructors))
            $(LI $(RELATIVE_LINK2 static-destructor, Static Destructors))
            $(LI $(GLINK SharedStaticConstructor)s)
            $(LI $(GLINK SharedStaticDestructor)s)
            $(LI $(RELATIVE_LINK2 invariants, Class Invariants))
            $(LI $(DDSUBLINK spec/unittest, unittest, Unit Tests))
            $(LI $(RELATIVE_LINK2 allocators, Class Allocators))
            $(LI $(RELATIVE_LINK2 deallocators, Class Deallocators))
            $(LI $(RELATIVE_LINK2 alias-this, Alias This))
        )
        )
)

$(P A class is defined:)

------
class Foo
{
    ... members ...
}
------

        $(P Note that there is no trailing $(D ;) after the closing $(D }) of the class
        definition.
        It is also not possible to declare a variable var like:)

------
class Foo { } var;
------

        Instead:

------
class Foo { }
Foo var;
------

$(H2 $(LNAME2 access_control, Access Control))

        $(P Access to class members is controlled using
        $(DDSUBLINK spec/attribute, visibility_attributes,
        visibility attributes).
        The default visibility attribute is $(D public).
        )

$(H2 $(LNAME2 fields, Fields))

        $(P Class members are always accessed with the `.` operator.
        )

        $(P Members of a base class can be accessed by prepending the name of
        the base class followed by a dot:)

---
class A { int a; int a2;}
class B : A { int a; }

void foo(B b)
{
    b.a = 3;   // accesses field B.a
    b.a2 = 4;  // accesses field A.a2
    b.A.a = 5; // accesses field A.a
}
---

        $(P The D compiler is free to rearrange the order of fields in a class to
        optimally pack them in an implementation-defined manner.
        Consider the fields much like the local
        variables in a function -
        the compiler assigns some to registers and shuffles others around all to
        get the optimal
        stack frame layout. This frees the code designer to organize the fields
        in a manner that
        makes the code more readable rather than being forced to organize it
        according to
        machine optimization rules. Explicit control of field layout is provided
        by struct/union
        types, not classes.
        )

        $(P Fields of `extern(Objective-C)` classes have a dynamic offset. That
        means that the base class can change (add or remove instance variables)
        without the subclasses needing to recompile or relink.
        )

$(H2 $(LNAME2 field_properties, Field Properties))

        $(P The $(D .offsetof) property gives the offset in bytes of the field
        from the beginning of the class instantiation.
        `.offsetof` is not available for fields of `extern(Objective-C)` classes
        due to their fields having a dynamic offset.
        $(D .offsetof) can only be applied to
        expressions which produce the type of
        the field itself, not the class type:
        )

------
class Foo
{
    int x;
}
...
void test(Foo foo)
{
    size_t o;

    o = Foo.x.offsetof; // error, Foo.x needs a 'this' reference
    o = foo.x.offsetof; // ok
}
------

$(H2 $(LNAME2 class_properties, Class Properties))

        $(P The $(D .tupleof) property is an
        $(DDSUBLINK spec/template, variadic-templates, expression sequence)
        of all the fields in the class, excluding the hidden fields and
        the fields in the base class.
        `.tupleof` is not available for `extern(Objective-C)` classes due to
        their fields having a dynamic offset.
        )
---
class Foo { int x; long y; }

void test(Foo foo)
{
    import std.stdio;
    static assert(typeof(foo.tupleof).stringof == `(int, long)`);

    foo.tupleof[0] = 1; // set foo.x to 1
    foo.tupleof[1] = 2; // set foo.y to 2
    foreach (x; foo.tupleof)
        write(x);       // prints 12
}
---

        $(P The properties $(D .__vptr) and $(D .__monitor) give access
        to the class object's vtbl[] and monitor, respectively, but
        should not be used in user code.
        )

$(H2 $(LNAME2 super_class, Super Class))

        All classes inherit from a super class. If one is not specified,
        it inherits from $(REF1 Object, object). `Object` forms the root
        of the D class inheritance hierarchy.

$(H2 $(LNAME2 member-functions, Member Functions (a.k.a. Methods)))

        $(P Non-static member functions have an extra hidden parameter
        called $(I this) through which the class object's other members
        can be accessed.
        )

        $(P Non-static member functions can have, in addition to the usual
        $(GLINK2 function, FunctionAttribute)s, the attributes
        $(D const), $(D immutable), $(D shared), or $(D inout).
        These attributes apply to the hidden $(I this) parameter.
        )
---
class C
{
    int a;
    const void foo()
    {
        a = 3; // error, 'this' is const
    }
    void foo() immutable
    {
        a = 3; // error, 'this' is immutable
    }
}
---

$(H3 $(LNAME2 objc-member-functions, Objective-C linkage))

        $(P Static member functions with
        `Objective-C` linkage also have an extra hidden parameter called $(I this)
        through which the class object's other members can be accessed.
        )

        $(P Member functions with Objective-C linkage have an additional
        hidden, anonymous, parameter which is the selector the function was
        called with.
        )

        $(P Static member functions with Objective-C linkage are placed in
        the hidden nested metaclass as non-static member functions.
        )


$(H2 $(LNAME2 synchronized-classes, Synchronized Classes))

        $(P All member functions of synchronized classes are synchronized.
        A static member function is synchronized on the $(I classinfo)
        object for the class, which means that one monitor is used
        for all static member functions for that synchronized class.
        For non-static functions of a synchronized class, the monitor
        used is part of the class object. For example:
        )

        ---
        synchronized class Foo
        {
            void bar() { ...statements... }
        }
        ---

        $(P is equivalent to (as far as the monitors go):
        )

        ---
        synchronized class Foo
        {
            void bar()
            {
                synchronized (this) { ...statements... }
            }
        }
        ---

        $(P Member functions of non-synchronized classes can be individually marked as synchronized.
        )

        ---
        class Foo
        {
            synchronized void foo() { }  // foo is synchronized
        }
        ---

        $(P Member fields of a synchronized class cannot be public:
        )

        ---
        synchronized class Foo
        {
            int foo;  // disallowed: public field
        }

        synchronized class Bar
        {
            private int bar;  // ok
        }
        ---

        $(P The $(D synchronized) attribute can only be applied to classes,
        structs cannot be marked to be synchronized.)

$(H2 $(LNAME2 constructors, Constructors))

$(GRAMMAR
$(GNAME Constructor):
    $(D this) $(GLINK2 function, Parameters) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D this) $(GLINK2 function, Parameters) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
    $(GLINK2 template, ConstructorTemplate)
)

        $(P Fields are by default initialized to the
        $(LNAME2 class-default-initializer, default initializer)
        for their type (usually 0 for integer types and
        NAN for floating point types).
        If the field declaration has an optional $(GLINK2 declaration, Initializer)
        that will be used instead of the default.
        )
        ------
        class Abc
        {
            int a;      // default initializer for a is 0
            long b = 7; // default initializer for b is 7
            float f;    // default initializer for f is NAN
        }
        ------

        $(P The $(I Initializer) is evaluated at compile time.)

        $(P This initialization is done before any constructors are
        called.)

        $(P Constructors are defined with a function name of $(D this)
        and have no return value:)

        ------
        class Foo
        {
            $(CODE_HIGHLIGHT this)(int x)  // declare constructor for Foo
            {   ...
            }
            $(CODE_HIGHLIGHT this)()
            {   ...
            }
        }
        ------

        $(P Base class construction is done by calling the base class
        constructor by the name $(D super):)

        ------
        class A { this(int y) { } }

        class B : A
        {
            int j;
            this()
            {
                ...
                $(CODE_HIGHLIGHT super)(3);  // call base constructor A.this(3)
                ...
            }
        }
        ------

$(H3 $(LNAME2 delegating-constructors, Delegating Constructors))

        $(P A constructor can call another constructor for the same class
        in order to share common initializations.
        This is called a $(I delegating constructor):
        )

        ------
        class C
        {
            int j;
            this()
            {
                ...
            }
            this(int i)
            {
                $(CODE_HIGHLIGHT this)(); // delegating constructor call
                j = i;
            }
        }
        ------

        $(P The following restrictions apply:)

    $(OL
        $(LI It is illegal for constructors to mutually call each other.

        ------
        this() { this(1); }
        this(int i) { this(); } // illegal, cyclic constructor calls
        ------

        $(IMPLEMENTATION_DEFINED The compiler is not required to detect
        cyclic constructor calls.)

        $(UNDEFINED_BEHAVIOR If the program executes with cyclic constructor
        calls.)
        )

        $(LI If a constructor's code contains a delegating constructor call, all
        possible execution paths through the constructor must make exactly one
        delegating constructor call:

        ------
        this() { a || super(); }       // illegal

        this() { (a) ? this(1) : super(); }     // ok

        this()
        {
            for (...)
            {
                super();  // illegal, inside loop
            }
        }
        ------
        )

        $(LI It is illegal to refer to $(D this) implicitly or explicitly
        prior to making a delegating constructor call.)

        $(LI Delegating constructor calls cannot appear after labels.)
    )

$(H3 $(LNAME2 implicit-base-construction, Implicit Base Class Construction))

        $(P If there is no constructor for a class, but there is a constructor
        for the base class, a default constructor is implicitly generated with
        the form:)

        ------
        this() { }
        ------

        $(P If no calls to a delegating constructor or $(D super) appear in a
        constructor, and the base class has a nullary constructor, a call to
        `super()` is inserted at the beginning of the constructor. If that
        base class has a constructor that requires arguments and no
        nullary constructor, a matching call to `super` is required.)

$(H3 $(LNAME2 class-instantiation, Class Instantiation))

        $(P Instances of class objects are created with a $(GLINK2 expression, NewExpression):)

        ------
        A a = new A(3);
        ------

        $(P The following steps happen:)

    $(OL
        $(LI Storage is allocated for the object.
        If this fails, rather than return $(D null), an
        $(LINK2 $(ROOT_DIR)library/core/exception/out_of_memory_error.html, OutOfMemoryError) is thrown.
        Thus, tedious checks for null references are unnecessary.
        )

        $(LI The raw data is statically initialized using the values provided
        in the class definition.
        The pointer to the vtbl[] (the array of pointers to virtual functions)
        is assigned.
        Constructors are
        passed fully formed objects for which virtual functions can be called.
        This operation is equivalent to doing a memory copy of a static
        version of the object onto the newly allocated one.
        )

        $(LI If there is a constructor defined for the class,
        the constructor matching the
        argument list is called.
        )

        $(LI If a delegating constructor is not called, a call to the base
        class's default constructor is issued.)

        $(LI The body of the constructor is executed.)

        $(LI If class invariant checking is turned on, the class invariant
        is called at the end of the constructor.
        )
    )

$(H3 $(LNAME2 constructor-attributes, Constructor Attributes))

        $(P Constructors can have one of these member function attributes:
        $(D const), $(D immutable), and $(D shared). Construction of qualified
        objects will then be restricted to the implemented qualified constructors.
        )
        ------
        class C
        {
            this();   // non-shared mutable constructor
        }

        // create mutable object
        C m = new C();

        // create const object using mutable constructor
        const C c2 = new const C();

        // a mutable constructor cannot create an immutable object
        // immutable C i = new immutable C();

        // a mutable constructor cannot create a shared object
        // shared C s = new shared C();
        ------

        $(P Constructors can be overloaded with different attributes.
        )
        ------
        class C
        {
            this();               // non-shared mutable constructor
            this() shared;        // shared mutable constructor
            this() immutable;     // immutable constructor
        }

        C m = new C();
        shared s = new shared C();
        immutable i = new immutable C();
        ------

$(H4 $(LNAME2 pure-constructors, Pure Constructors))

        $(P If the constructor can create a unique object (e.g. if it is `pure`),
        the object can be implicitly convertible to any qualifiers.
        )
        ------
        class C
        {
            this() pure;
            // Based on the definition, this creates a mutable object. But the
            // created object cannot contain any mutable global data.
            // Therefore the created object is unique.

            this(int[] arr) immutable pure;
            // Based on the definition, this creates an immutable object. But
            // the argument int[] never appears in the created object so it
            // isn't implicitly convertible to immutable. Also, it cannot store
            // any immutable global data.
            // Therefore the created object is unique.
        }

        immutable i = new immutable C();           // this() pure is called
        shared s = new shared C();                 // this() pure is called
        C m = new C([1,2,3]);       // this(int[]) immutable pure is called
        ------

$(H3 $(LNAME2 field-init, Field initialization inside a constructor))

        $(P In a constructor body, the first instance of field assignment is
        its initialization.
        )

        ------
        class C
        {
            int num;
            this()
            {
                num = 1;  // initialization
                num = 2;  // assignment
            }
        }
        ------

       $(P If the field type has an $(LINK2 operatoroverloading.html#assignment, `opAssign`)
       method, it will not be used for initialization.)

        ------
        struct A
        {
            this(int n) {}
            void $(CODE_HIGHLIGHT opAssign)(A rhs) {}
        }
        class C
        {
            A val;
            this()
            {
                val = A(1);  // val is initialized to the value of A(1)
                val = A(2);  // rewritten to val.opAssign(A(2))
            }
        }
        ------

        $(P If the field type is not mutable, multiple initialization will be rejected.)

        ------
        class C
        {
            immutable int num;
            this()
            {
                num = 1;  // OK
                num = 2;  // Error: multiple field initialization
            }
        }
        ------

        $(P If the field is initialized on one path, it must be initialized on all paths.)
        ---
        class C
        {
            immutable int num;
            immutable int ber;
            this(int i)
            {
                if (i)
                    num = 3;   // initialization
                else
                    num = 4;   // initialization
            }
            this(long j)
            {
                j ? (num = 3) : (num = 4); // ok
                j || (ber = 3);  // error, intialized on only one path
                j && (ber = 3);  // error, intialized on only one path
            }
        }
        ---

        $(P A field initialization may not appear in a loop or after
        a label.)

        ------
        class C
        {
            immutable int num;
            immutable string str;
            this()
            {
                foreach (i; 0..2)
                {
                    num = 1;    // Error: field initialization not allowed in loops
                }
                size_t i = 0;
            Label:
                str = "hello";  // Error: field initialization not allowed after labels
                if (i++ < 2)
                    goto Label;
            }
        }
        ------

        $(P If a field's type has disabled default construction, then it must be initialized
        in the constructor.)
        ---
        struct S { int y; @disable this(); }

        class C
        {
            S s;
            this(S t) { s = t; }    // ok
            this(int i) { this(); } // ok
            this() { }              // error, s not initialized
        }
        ---

$(H2 $(LNAME2 destructors, Destructors))

$(GRAMMAR
$(GNAME Destructor):
    $(D ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
)

        $(P The garbage collector calls the destructor function when the object
        is deleted. The syntax is:)

        ------
        class Foo
        {
            ~this() // destructor for Foo
            {
            }
        }
        ------

        $(P There can be only one destructor per class, the destructor
        does not have any parameters,
        and has no attributes. It is always virtual.
        )

        $(P The destructor is expected to release any resources held by the
        object.
        )

        $(P The program can explicitly inform the garbage collector that an
        object is no longer referred to with $(REF1 destroy, object), and
        then the garbage collector calls the destructor immediately.  The
        destructor is guaranteed to never be called twice.
        )

        $(P The destructor for the super class automatically gets called when
        the destructor ends. There is no way to call the super destructor
        explicitly.
        )

        $(P The garbage collector is not guaranteed to run the destructor
        for all unreferenced objects. Furthermore, the order in which the
        garbage collector calls destructors for unreferenced objects
        is not specified.
        This means that
        when the garbage collector calls a destructor for an object of a class
        that has
        members which are references to garbage collected objects, those
        references may no longer be valid. This means that destructors
        cannot reference sub objects.
        This rule does not apply to auto objects or objects destructed
        with $(REF1 destroy, object), as the destructor is not being run
        by the garbage collector, meaning all references are valid.
        )

        $(P Objects referenced from the data segment never get collected
        by the gc.
        )

$(H2 $(LNAME2 static-constructor, Static Constructors))

$(GRAMMAR
$(GNAME StaticConstructor):
    $(D static this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D static this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
)

        $(P A static constructor is a function that performs initializations of
        thread local data before the $(D main()) function gets control for the main
        thread, and upon thread startup.)

        $(P Static constructors are used to initialize static class members with
        values that cannot be computed at compile time.)

        $(P Static constructors in other languages are built implicitly by using
        member
        initializers that can't be computed at compile time. The trouble with
        this stems from not
        having good control over exactly when the code is executed, for example:
        )

------
class Foo
{
    static int a = b + 1;
    static int b = a * 2;
}
------

        What values do a and b end up with, what order are the initializations
        executed in, what
        are the values of a and b before the initializations are run, is this a
        compile error, or is this
        a runtime error? Additional confusion comes from it not being obvious if
        an initializer is
        static or dynamic.

        $(P D makes this simple. All member initializations must be determinable by
        the compiler at
        compile time, hence there is no order-of-evaluation dependency for
        member
        initializations, and it is not possible to read a value that has not
        been initialized. Dynamic
        initialization is performed by a static constructor, defined with
        a special syntax $(D static this()).)

------
class Foo
{
    static int a;         // default initialized to 0
    static int b = 1;
    static int c = b + a; // error, not a constant initializer

    $(CODE_HIGHLIGHT static this)()    // static constructor
    {
        a = b + 1;          // a is set to 2
        b = a * 2;          // b is set to 4
    }
}
------

$(P If $(D main()) or the thread returns normally, (does not throw an
exception), the static destructor is added to the list of functions to be called
on thread termination.)

$(P Static constructors have empty parameter lists.)

        $(P
        Static constructors within a module are executed in the lexical
        order in which they appear.
        All the static constructors for modules that are directly or
        indirectly imported
        are executed before the static constructors for the importer.
        )

        $(P
        The $(D static) in the static constructor declaration is not
        an attribute, it must appear immediately before the $(D this):
        )

------
class Foo
{
    static this() { ... } // a static constructor
    static private this() { ... } // not a static constructor
    static
    {
        this() { ... }      // not a static constructor
    }
    static:
        this() { ... }      // not a static constructor
}
------

$(H2 $(LNAME2 static-destructor, Static Destructors))

$(GRAMMAR
$(GNAME StaticDestructor):
    $(D static ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D static ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
)

        A static destructor is defined as a special static function with the
        syntax $(D static ~this()).

------
class Foo
{
    static ~this() // static destructor
    {
    }
}
------

        $(P
        A static destructor gets called on thread termination,
        but only if the static constructor
        completed successfully.
        Static destructors have empty parameter lists.
        Static destructors get called in the reverse order that the static
        constructors were called in.
        )

        $(P
        The $(D static) in the static destructor declaration is not
        an attribute, it must appear immediately before the $(D ~this):
        )

------
class Foo
{
    static ~this() { ... }  // a static destructor
    static private ~this() { ... } // not a static destructor
    static
    {
        ~this() { ... }  // not a static destructor
    }
    static:
        ~this() { ... }  // not a static destructor
}
------

$(H2 $(LNAME2 shared_static_constructors, Shared Static Constructors))

$(GRAMMAR
$(GNAME SharedStaticConstructor):
    $(D shared static this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D shared static this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
)

        $(P Shared static constructors are executed before any $(GLINK StaticConstructor)s,
        and are intended for initializing any shared global data.
        )

$(H2 $(LNAME2 shared_static_destructors, Shared Static Destructors))

$(GRAMMAR
$(GNAME SharedStaticDestructor):
    $(D shared static ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(D ;)
    $(D shared static ~ this ( )) $(GLINK2 function, MemberFunctionAttributes)$(OPT) $(GLINK2 function, FunctionBody)
)

        $(P Shared static destructors are executed at program termination
        in the reverse order that
        $(GLINK SharedStaticConstructor)s were executed.
        )


$(H2 $(LNAME2 invariants, Class Invariants))

$(GRAMMAR
$(GNAME ClassInvariant):
    $(D invariant $(LPAREN) $(RPAREN)) $(GLINK2 statement, BlockStatement)
    $(D invariant) $(GLINK2 statement, BlockStatement)
    $(D invariant $(LPAREN)) $(GLINK2 expression, AssertArguments) $(D $(RPAREN) ;)
)

    $(P $(I ClassInvariant) specify the relationships among the members of a class instance.
    Those relationships must hold for any interactions with the instance from its
    public interface.
    )

    $(P The invariant is in the form of a $(D const) member function. The invariant is defined
    to $(I hold) if all the $(GLINK2 expression, AssertExpression)s within the invariant that are executed
    succeed.
    )

    $(P If the invariant does not hold, then the program enters an invalid state.)

    $(P Any class invariants for base classes are applied before the class invariant for the derived class.)

    $(P There may be multiple invariants in a class. They are applied in lexical order.)

    $(P $(I ClassInvariant)s must hold at the exit of the class constructor (if any),
    at the entry of the class destructor (if any).)

    $(P $(I ClassInvariant)s must hold
    at the entry and exit of all public or exported non-static member functions.
    The order of application of invariants is:
    $(OL
    $(LI preconditions)
    $(LI invariant)
    $(LI function body)
    $(LI invariant)
    $(LI postconditions)
    )
    )

    $(P The invariant need not hold if the class instance is implicitly constructed using
    the default $(D .init) value.)

    ---
    class Date
    {
        this(int d, int h)
        {
            day = d;    // days are 1..31
            hour = h;   // hours are 0..23
        }

        invariant
        {
            assert(1 <= day && day <= 31);
            assert(0 <= hour && hour < 24);
        }

      private:
        int day;
        int hour;
    }
    ---

    $(P Public or exported non-static member functions cannot be called from within an invariant.)

    ---
    class Foo
    {
        public void f() { }
        private void g() { }

        invariant
        {
            f();  // error, cannot call public member function from invariant
            g();  // ok, g() is not public
        }
    }
    ---

    $(UNDEFINED_BEHAVIOR happens if the invariant does not hold and execution continues.)

    $(IMPLEMENTATION_DEFINED
    $(OL
    $(LI Whether the $(I Class Invariant) is executed at runtime or not. This is typically
    controlled with a compiler switch.)
    $(LI The behavior when the invariant does not hold is typically the same as
    for when $(GLINK2 expression, AssertExpression)s fail.)
    )
    )

    $(BEST_PRACTICE
    $(OL
    $(LI Do not indirectly call exported or public member functions within a class invariant,
    as this can result in infinite recursion.)
    $(LI Avoid reliance on side effects in the invariant. as the invariant may or may not
    be executed.)
    $(LI Avoid having mutable public fields of classes with invariants,
    as then the invariant cannot verify the public interface.)
    )
    )


$(H2 $(LNAME2 allocators, Class Allocators))
$(B Note): Class allocators are deprecated in D2.
$(GRAMMAR
$(GNAME Allocator):
    $(D new) $(GLINK2 function, Parameters) $(D ;)
    $(D new) $(GLINK2 function, Parameters) $(GLINK2 function, FunctionBody)
)

$(P A class member function of the form:)

------
new(size_t size)
{
    ...
}
------

        is called a class allocator.
        The class allocator can have any number of parameters, provided
        the first one is of type `size_t`.
        Any number can be defined for a class, the correct one is
        determined by the usual function overloading rules.
        When a new expression:

------
new Foo;
------

        is executed, and Foo is a class that has
        an allocator, the allocator is called with the first argument
        set to the size in bytes of the memory to be allocated for the
        instance.
        The allocator must allocate the memory and return it as a
        $(D void*).
        If the allocator fails, it must not return a $(D null), but
        must throw an exception.
        If there is more than one parameter to the allocator, the
        additional arguments are specified within parentheses after
        the $(D new) in the $(I NewExpression):

------
class Foo
{
    this(string a) { ... }

    new(size_t size, int x, int y)
    {
        ...
    }
}

...

new(1,2) Foo(a);        // calls new(Foo.sizeof,1,2)
------

        $(P Derived classes inherit any allocator from their base class,
        if one is not specified.
        )

        $(P The class allocator is not called if the instance is created
        on the stack.
        )

        $(P See also
        $(LINK2 https://wiki.dlang.org/Memory_Management#Explicit_Class_Instance_Allocation,
        Explicit Class Instance Allocation).
        )

$(H2 $(LNAME2 deallocators, Class Deallocators))
$(B Note): Class deallocators and the delete operator are deprecated in D2.
Use the $(D destroy) function to finalize an object by calling its destructor.
The memory of the object is $(B not) immediately deallocated, instead the GC
will collect the memory of the object at an undetermined point after finalization:

------
class Foo { int x; this() { x = 1; } }
Foo foo = new Foo;
destroy(foo);
assert(foo.x == int.init);  // object is still accessible
------

$(GRAMMAR
$(GNAME Deallocator):
    $(D delete) $(GLINK2 function, Parameters) $(D ;)
    $(D delete) $(GLINK2 function, Parameters) $(GLINK2 function, FunctionBody)
)

$(P A class member function of the form:)

------
delete(void *p)
{
    ...
}
------

        is called a class deallocator.
        The deallocator must have exactly one parameter of type $(D void*).
        Only one can be specified for a class.
        When a delete expression:

------
delete f;
------

is executed, and f is a reference to a class instance that has a deallocator,
the deallocator is called with a pointer to the class instance after the
destructor (if any) for the class is called. It is the responsibility of the
deallocator to free the memory.

        $(P Derived classes inherit any deallocator from their base class,
        if one is not specified.
        )

        $(P The class allocator is not called if the instance is created
        on the stack.
        )

        $(P See also
        $(LINK2 https://wiki.dlang.org/Memory_Management#Explicit_Class_Instance_Allocation,
        Explicit Class Instance Allocation).
        )

$(H2 $(LEGACY_LNAME2 AliasThis, alias-this, Alias This))

$(GRAMMAR
$(GNAME AliasThis):
    $(D alias) $(GLINK_LEX Identifier) $(D this ;)
)

        $(P An $(I AliasThis) declaration names a member to subtype.
        The $(I Identifier) names that member.
        )

        $(P A class or struct can be implicitly converted to the $(I AliasThis)
        member.
        )

---
struct S
{
    int x;
    alias x this;
}

int foo(int i) { return i * 2; }

void test()
{
    S s;
    s.x = 7;
    int i = -s;  // i == -7
    i = s + 8;   // i == 15
    i = s + s;   // i == 14
    i = 9 + s;   // i == 16
    i = foo(s);  // implicit conversion to int
}
---

        $(P If the member is a class or struct, undefined lookups will
        be forwarded to the $(I AliasThis) member.
        )

---
struct Foo
{
    int baz = 4;
    int get() { return 7; }
}

class Bar
{
    Foo foo;
    alias foo this;
}

void test()
{
    auto bar = new Bar;
    int i = bar.baz; // i == 4
    i = bar.get(); // i == 7
}
---

        $(P If the $(I Identifier) refers to a property member
        function with no parameters, conversions and undefined
        lookups are forwarded to the return value of the function.
        )

---
struct S
{
    int x;
    @property int get()
    {
        return x * 2;
    }
    alias get this;
}

void test()
{
    S s;
    s.x = 2;
    int i = s; // i == 4
}
---
        $(P If an aggregate declaration defines an $(D opCmp) or $(D opEquals)
        method, it will take precedence to that of the aliased this member. Note
        that, unlike an $(D opCmp) method, an $(D opEquals) method is implicitly
        defined for a $(D struct) declaration if a user defined one isn't provided;
        this means that if the aliased this member $(D opEquals) is preferred it
        should be explicitly defined:
        )
---
struct S
{
    int a;
    bool opEquals(S rhs) const
    {
        return this.a == rhs.a;
    }
}

struct T
{
    int b;
    S s;
    alias s this;
}

struct U
{
    int a;
    bool opCmp(U rhs) const
    {
        return this.a < rhs.a;
    }
}

struct V
{
    int b;
    U u;
    alias u this;
}

void main()
{
    S s1, s2;
    T t1, t2;
    U u1, u2;
    V v1, v2;

    assert(s1 == s2);      // calls S.opEquals
    assert(t1 == t2);      // calls compiler generated T.opEquals that implements member-wise equality
    assert(!(u1 < u2));    // calls U.opCmp
    assert(!(v1 < v2));    // calls U.opCmp because V does not define an opCmp method
                           // so the alias this of v1 is employed; U.opCmp expects a
                           // paramter of type U, so alias this of v2 is used

    assert(s1 == t1);      // calls s1.opEquals(t1.s);
    assert(t1 == s1);      // calls t1.s.opEquals(s1);
    assert(!(u1 < v1));    // calls u1.opCmp(v1.u);
    assert(!(v1 < u1));    // calls v1.u.opCmp(v1);
}
---
        $(P $(GLINK2 attribute, Attribute)s are ignored for $(D AliasThis).
        )

        $(P Multiple $(I AliasThis) are currently not allowed.)

$(H2 $(LNAME2 auto, Scope Classes))
$(B Note): Scope classes have been $(DDSUBLINK deprecate, scope for allocating classes on the stack, recommended for deprecation).

        $(P A scope class is a class with the $(D scope) attribute, as in:)

------
scope class Foo { ... }
------

$(P The scope characteristic is inherited, so any classes derived from a scope
class are also scope.)

        $(P A scope class reference can only appear as a function local variable.
        It must be declared as being $(D scope):)

------
scope class Foo { ... }

void func()
{
    Foo f;    // error, reference to scope class must be scope
    scope Foo g = new Foo(); // correct
}
------

$(P When a scope class reference goes out of scope, the destructor (if any) for
it is automatically called. This holds true even if the scope was exited via a
thrown exception.)

$(H2 $(LNAME2 final, Final Classes))

        $(P Final classes cannot be subclassed:)

---
final class A { }
class B : A { }  // error, class A is final
---

        $(P Methods of a final class are `final` by default.)

$(H2 $(LNAME2 nested, Nested Classes))

$(P A $(I nested class) is a class that is declared inside the scope of a
function or another class. A nested class has access to the variables and other
symbols of the classes and functions it is nested inside:)

------
class Outer
{
    int m;

    class Inner
    {
        int foo()
        {
            return m;   // Ok to access member of Outer
        }
    }
}

void func()
{
    int m;

    class Inner
    {
        int foo()
        {
            return m; // Ok to access local variable m of func()
        }
    }
}
------

$(H3 $(LNAME2 static-nested, Static Nested Classes))

$(P If a nested class has the $(D static) attribute, then it can not access
variables of the enclosing scope that are local to the stack or need a
`this` reference:)

------
class Outer
{
    int m;
    static int n;

    static class Inner
    {
        int foo()
        {
            return m;   // Error, Inner is static and m needs a this
            return n;   // Ok, n is static
        }
    }
}

void func()
{
    int m;
    static int n;

    static class Inner
    {
        int foo()
        {
            return m;   // Error, Inner is static and m is local to the stack
            return n;   // Ok, n is static
        }
    }
}
------

$(H3 $(LNAME2 nested-context, Context Pointer))

$(P Non-static nested classes work by containing an extra hidden member (called
the context pointer) that is the frame pointer of the enclosing function if it
is nested inside a function, or the $(D this) reference of the enclosing class's instance
if it is nested inside a class.)

        $(P When a non-static nested class is instantiated, the context pointer
        is assigned before the class's constructor is called, therefore
        the constructor has full access to the enclosing variables.
        A non-static nested class can only be instantiated when the necessary
        context pointer information is available:)

------
class Outer
{
    class Inner { }

    static class SInner { }
}

void func()
{
    class Nested { }

    Outer o = new Outer;        // Ok
    Outer.Inner oi = new Outer.Inner;   // Error, no 'this' for Outer
    Outer.SInner os = new Outer.SInner; // Ok

    Nested n = new Nested;      // Ok
}
------

$(H3 $(LNAME2 nested-explicit, Explicit Instantiation))

        $(P A `this` reference can be supplied to the creation of an
        inner class instance by prefixing it to the $(I NewExpression):
        )

---------
class Outer
{
    int a;

    class Inner
    {
        int foo()
        {
            return a;
        }
    }
}

int bar()
{
    Outer o = new Outer;
    o.a = 3;
    Outer.Inner oi = $(CODE_HIGHLIGHT o).new Inner;
    return oi.foo();    // returns 3
}
---------

        $(P Here $(D o) supplies the `this` reference to the inner class
        instance of $(D Outer).
        )

$(H3 $(LNAME2 outer-property, `outer` Property))

        $(P For a nested class instance, the $(D .outer) property is the
        $(D this) reference for the enclosing class's instance. If there
        is no enclosing class context, $(D .outer) would be a $(D void*)
        to the enclosing function frame.
        )

----
class Outer
{
    class Inner1
    {
        Outer getOuter()
        {
            return this.$(CODE_HIGHLIGHT outer);
        }
    }

    void foo()
    {
        Inner1 i = new Inner1;
        assert(i.getOuter() is this);
    }

    void bar()
    {
        // x is referenced from nested scope, so
        // bar makes a closure environment.
        int x = 1;

        class Inner2
        {
            Outer getOuter()
            {
                x = 2;
                // The Inner2 instance has access to the function frame
                // of bar as a static frame pointer, but .outer returns
                // the enclosing Outer class instance property.
                return this.$(CODE_HIGHLIGHT outer);
            }
        }

        Inner2 i = new Inner2;
        assert(i.getOuter() is this);
    }

    // baz cannot access an instance of Outer
    static void baz()
    {
        // make a closure environment
        int x = 1;

        class Inner3
        {
            void* getOuter()
            {
                x = 2;
                // There's no accessible enclosing class instance, so the
                // .outer property returns the function frame of baz.
                return this.$(CODE_HIGHLIGHT outer);
            }
        }

        Inner3 i = new Inner3;
        assert(i.getOuter() !is null);
    }
}
----

$(H3 $(LNAME2 anonymous, Anonymous Nested Classes))

        $(P An anonymous nested class is both defined and instantiated with
        a $(I NewAnonClassExpression):
        )

$(GRAMMAR
$(GNAME NewAnonClassExpression):
    $(D new) $(GLINK2 expression, AllocatorArguments)$(OPT) $(D class) $(I ConstructorArgs)$(OPT) $(GLINK SuperClass)$(OPT) $(GLINK Interfaces)$(OPT) $(GLINK2 struct, AggregateBody)

$(GNAME ConstructorArgs):
    $(D $(LPAREN)) $(GLINK2 expression, ArgumentList)$(OPT) $(D $(RPAREN))
)

which is equivalent to:

$(GRAMMAR_INFORMATIVE
$(D class) $(GLINK_LEX Identifier) $(D :) $(I SuperClass) $(I Interfaces) $(I AggregateBody)
// ...
$(D new) $(I AllocatorArguments) $(I Identifier) $(I ConstructorArgs)
)

where $(I Identifier) is the name generated for the anonymous nested class.

$(SPEC_RUNNABLE_EXAMPLE_RUN
---
interface I
{
    void foo();
}

auto obj = new class I
{
    void foo()
    {
        writeln("foo");
    }
};
obj.foo();
---
)

$(SECTION2 $(LEGACY_LNAME2 ConstClass, const-class, $(ARGS Const, Immutable and Shared Classes)),

    $(P If a $(I ClassDeclaration) has a $(D const), $(D immutable)
        or $(D shared) storage class, then it is as if each member of the class
        was declared with that storage class.
        If a base class is const, immutable or shared, then all classes derived
        from it are also const, immutable or shared.
    )
)
$(SPEC_SUBNAV_PREV_NEXT struct, Structs and Unions, interface, Interfaces)
)

Macros:
        CHAPTER=15
        TITLE=Classes
        _=
